`timescale 1ns / 1ps
`include "macro.v"

module Control_Unit (
    input [31:0] Instr,
    input [2:0] State,//å½“å‰æµæ°´çº§æ•°çš„è¾“ï¿
    output [15:0] Imm,
    output [25:0] Instr_index,
    output [4:0] rs,
    output [4:0] rt,
    output [4:0] rd, 
    output [4:0] s,
    output [1:0] RegDst,
    output RegWrite,
    output CP0Write,
    output [3:0] Branch,
    output EXTop,
    output [1:0] Shiftop,
    output ALUSrc,
    output [4:0] ALUop,
    output Start,
    output [2:0] MADop,
    output [2:0] BEop,
    output [2:0] DMEXTop,
    output [2:0] Move,
    output [2:0] Tnew,
    output [2:0] Tuse_rs,
    output [2:0] Tuse_rt,
    output HILO,//è¡¨ç¤ºæŒ‡ä»¤éœ€è¦ç”¨åˆ°HI/LOå¯„å­˜ï¿
    output [3:0] D_RDMUXSrc,
    output [3:0] E_RDMUXSrc,
    output [3:0] M_RDMUXSrc,
    output RI,
    output eret);
 // å°†æŒ‡ä»¤è¿›è¡Œè¯‘ï¿
    wire [5:0] op,funct;

    assign Imm = Instr[15:0];
    assign Instr_index = Instr[25:0];
    assign op = Instr[31:26];
    assign funct = Instr[5:0];
    assign rs = Instr[26:21];
    assign rt = Instr[20:16];
    assign rd = Instr[15:11];
    assign s = Instr[10:6];

 //è¿›è¡ŒåŠŸèƒ½ä¿¡å·çš„è¯‘ç ï¼Œè¿™éƒ¨åˆ†ä¸Žå•å‘¨æœŸç›¸ï¿
   wire add = (op == `Rtype) && (funct == `add_func);
   wire addu = (op == `Rtype) && (funct == `addu_func);
   wire sub = (op == `Rtype) && (funct == `sub_func);
   wire subu = (op == `Rtype) && (funct == `subu_func);
   wire mult = (op == `Rtype) && (funct == `mult_func);
   wire multu = (op == `Rtype) && (funct == `multu_func);
   wire div = (op == `Rtype) && (funct == `div_func);
   wire divu = (op == `Rtype) && (funct == `divu_func);
   wire sll = (op == `Rtype) && (funct == `sll_func);
   wire srl = (op == `Rtype) && (funct == `srl_func);
   wire sra = (op == `Rtype) && (funct == `sra_func);
   wire sllv = (op == `Rtype) && (funct == `sllv_func);
   wire srlv = (op == `Rtype) && (funct == `srlv_func);
   wire srav = (op == `Rtype) && (funct == `srav_func);
   wire instr_and = (op == `Rtype) && (funct == `and_func);
   wire instr_or = (op == `Rtype) && (funct == `or_func);
   wire instr_xor = (op == `Rtype) && (funct == `xor_func);
   wire instr_nor = (op == `Rtype) && (funct == `nor_func);
   wire slt = (op == `Rtype) && (funct == `slt_func);
   wire sltu = (op == `Rtype) && (funct == `sltu_func);
   wire jalr = (op == `Rtype) && (funct == `jalr_func);
   wire jr = (op == `Rtype) && (funct == `jr_func);
   wire mfhi = (op == `Rtype) && (funct == `mfhi_func);
   wire mflo = (op == `Rtype) && (funct == `mflo_func);
   wire mthi = (op == `Rtype) && (funct == `mthi_func);
   wire mtlo = (op == `Rtype) && (funct == `mtlo_func);

   wire lb = (op == `lb);
   wire lbu = (op == `lbu);
   wire lh = (op == `lh);
   wire lhu = (op == `lhu);
   wire lw = (op == `lw);
   wire sb = (op == `sb);
   wire sh = (op == `sh);
   wire sw = (op == `sw);
   wire addi = (op == `addi);
   wire addiu = (op == `addiu);
   wire andi = (op == `andi);
   wire ori = (op == `ori);
   wire xori = (op == `xori);
   wire lui = (op == `lui);
   wire slti = (op == `slti);
   wire sltiu = (op == `sltiu);
   wire beq = (op == `beq);
   wire bne = (op == `bne);
   wire blez = (op == `blez);
   wire bgtz = (op == `bgtz);
   wire bltz = (op == `bltz) && (rt == `bltz_rt);
   wire bgez = (op == `bgez) && (rt == `bgez_rt);
   wire j = (op == `j);
   wire jal = (op == `jal);
   wire mfc0 = (op == `COP0) && (rs == `mfc0_rs);
   wire mtc0 = (op == `COP0) && (rs == `mtc0_rs);
   assign eret = (op == `COP0) && (funct == `eret_func);  

//å°†æŒ‡ä»¤è¿›è¡Œåˆ†ç±»ï¼Œæ–¹ä¾¿æŽ§åˆ¶ä¿¡å·çš„ç”Ÿï¿½æŒ‡ä»¤çš„åˆ†ç±»æ˜¯ä¸ºäº†æ–¹ä¾¿æ•´ä½“æ•°æ®é€šè·¯çš„é€‰æ‹©ï¼Œè‡³äºŽéƒ¨ä»¶å†…éƒ¨çš„é€‰æ‹©ä¿¡å·ï¼Œè¿˜æ˜¯éœ€è¦ä¾æ®ä¸åŒçš„æŒ‡ä»¤è¿›è¡Œä¸åŒçš„æ“ï¿
    wire calc_r = add || addu || sub || subu || instr_and || instr_or || instr_xor || instr_nor || slt || sltu;//råž‹è®¡ç®—æŒ‡ï¿
    wire calc_i = addi || addiu || andi || ori || xori || slti || sltiu;//iåž‹è®¡ç®—æŒ‡ä»¤é™¤lui)
    wire load = lb || lbu || lh || lhu || lw;
    wire store = sb || sh || sw;
    wire branch = beq || bne || blez || bgtz || bltz || bgez;
    wire shiftS = sll || srl || sra;
    wire shiftV = sllv || srlv || srav;
    wire j_r = jr || jalr;
    wire j_addr = j || jal;
    wire j_link = jal || jalr;
    wire m_delay = mult || multu || div || divu;//æœ‰å»¶è¿Ÿçš„ä¹˜é™¤ï¿
    wire mf = mfhi || mflo;//è¯»HI/LOå¯„å­˜ï¿½æ•°æ®å†™å…¥rdå­—æ®µå¯„å­˜ï¿
    wire mt = mthi || mtlo;//å†™HI/LOå¯„å­˜ï¿

// some information about the exception
    assign RI = !(calc_r || calc_i || load || store || branch || shiftS || shiftV || j_r || j_addr || m_delay || mf || mt || lui || mfc0 || mtc0 || eret);
    
//æŽ§åˆ¶ä¿¡å·çš„ç¼–ï¿½æ•°æ®é€šè·¯ä¸Žéƒ¨ä»¶å†…éƒ¨ç›¸åŒºåˆ«)    
    assign RegDst = (calc_r || shiftS || shiftV || mf || jalr) ? `rd :
                    (jal) ? `ra : `rt;
    assign RegWrite = calc_r || calc_i || load || lui || j_link || mf || shiftS || shiftV || mfc0;
    assign CP0Write = mtc0;
    
    assign Branch = (beq) ? `BEQ :
                    (bne) ? `BNE :
                    (blez) ? `BLEZ :
                    (bgtz) ? `BGTZ :
                    (bltz) ? `BLTZ :
                    (bgez) ? `BGEZ : `Branch_Default; 

    assign EXTop =  addi || addiu || slti || sltiu || load || store || branch;//éœ€è¦ç¬¦å·æ‰©å±•ç«‹å³æ•°çš„æŒ‡ï¿
    assign Shiftop = (branch) ? `two_bits_lshift : 
                     (lui) ? `sixteen_bits_lshift : `Shiftop_Default;

    assign ALUSrc = lui || calc_i || branch || load || store;//é€‰æ‹©è¾“å…¥ç«‹å³æ•°è¿˜æ˜¯å¯„å­˜å™¨
    assign ALUop = (instr_and || andi) ? `AND :
                   (instr_or || ori) ? `OR :
                   (instr_xor || xori) ? `XOR :
                   (instr_nor) ? `NOR :
                   (add || addi || load || store) ? `ADD :
                   (addu || addiu) ? `ADDU :
                   (sub) ? `SUB :
                   (subu) ? `SUBU :
                   (slt || slti) ? `SLT :
                   (sltu || sltiu) ? `SLTU :
                   (sll) ? `SLL :
                   (srl) ? `SRL :
                   (sra) ? `SRA :
                   (sllv) ? `SLLV :
                   (srlv) ? `SRLV :
                   (srav) ? `SRAV : `ALU_Default;
    
    assign Start = m_delay;
    assign MADop = (mult) ? `MULT :
                   (multu) ? `MULTU :
                   (div) ? `DIV :
                   (divu) ? `DIVU :
                   (mthi) ? `HI_Write :
                   (mtlo) ? `LO_Write : `MAD_Default;
    
    assign BEop = (sw) ? `SW :
                  (sh) ? `SH :
                  (sb) ? `SB : `Store_Default;
    assign DMEXTop = (lbu) ? `LBU :
                     (lb) ? `LB :
                     (lhu) ? `LHU :
                     (lh) ? `LH : 
                     (lw) ? `LW : `Load_Default;
    
   assign Move = (branch) ? `Branch :
                 (j_addr) ? `Index :
                 (j_r) ? `Register : `Move_Default;
   //å„çº§æµæ°´çš„å¯„å­˜å™¨æ•°æ®é€‰æ‹©ä¿¡å·
   assign D_RDMUXSrc = (j_link) ? `PC8 :
                       (lui) ? `LuiData : `RD_Default;
   assign E_RDMUXSrc = (calc_r || calc_i || shiftS || shiftV) ? `ALUResult : 
                       (mfhi) ? `HIResult : 
                       (mflo) ? `LOResult : `Front_RegData;

   assign M_RDMUXSrc = (load) ? `DMResult : 
                       (mfc0) ? `CP0Result : `Front_RegData;                                  

//è¿›è¡ŒATä¿¡å·çš„ç”Ÿï¿
    reg  [2:0] tnew, tuse_rs, tuse_rt;//æ¯æ¡æŒ‡ä»¤æœ€å¤šå†™ä¸€æ¡å¯„å­˜å™¨ï¼Œå› æ­¤åªéœ€ä¸€ä¸ªTnew
    //å°†æŒ‡ä»¤è¿›è¡Œä¸€ä¸ªåˆ†ç±»ï¼Œæ–¹ä¾¿è®¡ç®—
    wire ALU = calc_r || calc_i || shiftS || shiftV;
    wire MAD = mf;
    wire EXT = lui;
    wire Link = j_link;
    wire DM = load;
    wire CP0 = mfc0;

    always @(*) begin
        tnew = 0;
        tuse_rs = 5;//default
        tuse_rt = 5;
        if(ALU)
            tnew = (`D_ALU_Tnew > State) ? `D_ALU_Tnew - State : 0;
        else if(MAD)
            tnew = (`D_MAD_Tnew > State) ? `D_MAD_Tnew - State : 0;
        else if(EXT)
            tnew = (`D_EXT_Tnew > State) ? `D_EXT_Tnew - State : 0;
        else if(Link)
            tnew = (`D_Link_Tnew > State) ? `D_Link_Tnew - State : 0;        
        else if (DM)
            tnew = (`D_DM_Tnew > State) ? `D_DM_Tnew - State : 0;
        else if (CP0)
            tnew = (`D_CP0_Tnew > State) ? `D_CP0_Tnew - State : 0;    

        if(calc_r || shiftV || m_delay) begin
            tuse_rs = `E_level;
            tuse_rt = `E_level; 
        end
        else if(calc_i || load || mt) begin
            tuse_rs = `E_level;
        end
		else if(shiftS) begin
		     tuse_rt = `E_level;
		end
        else if(store) begin
            tuse_rs = `E_level;
            tuse_rt = `M_level;
        end
        else if(beq || bne) begin
            tuse_rs = `D_level;
            tuse_rt = `D_level;
        end
        else if(j_r || bgez || bgtz || blez || bltz) begin
            tuse_rs = `D_level;
        end
        else if(mtc0) begin
            tuse_rt = `M_level;
        end
    end    
    
    assign Tnew = tnew;
    assign Tuse_rs = tuse_rs;
    assign Tuse_rt = tuse_rt;

    assign HILO = m_delay || mf || mt;

endmodule